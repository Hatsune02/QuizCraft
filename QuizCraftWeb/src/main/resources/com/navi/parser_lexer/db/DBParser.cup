package com.navi.quizcraftweb.backend.parser_lexer.db;
import java_cup.runtime.*;
import java.util.*;
import java.text.DecimalFormat;
import com.navi.quizcraftweb.backend.parser_lexer.*;
import com.navi.quizcraftweb.backend.model.*;
import com.navi.quizcraftweb.backend.parser_lexer.db.objs.Position;

parser code
{:
    public ArrayList<Position> positions = new ArrayList<>();
    public Position finalPos = new Position();
    public ArrayList<Position> positionsTrivia = new ArrayList<>();
    public Position finalPosTrivia = new Position();
    public HashMap<String, Position> positionsComponents = new HashMap<>();
    public HashMap<String, Position> finalPosComponent = new HashMap<>();

    public ArrayList<String> idsUser = new ArrayList<>();
    public ArrayList<User> users = new ArrayList<>();

    public ArrayList<String> idsTrivia = new ArrayList<>();
    public ArrayList<Trivia> trivias = new ArrayList<>();

    public HashMap<String, ArrayList<String>> idsComponent = new HashMap<>();

    public DBParser(DBLexer lex) {
        super(lex);
    }
    //Metodo al que se llama ante algun error sintactico
    public void syntax_error(Symbol s){
        String lexeme = s.value.toString();
        int line = s.left;
        int col = s.right;
        System.out.println("ERROR SINTÁCTICO RECUPERADO");
        System.out.println("\tLexeme: " + lexeme);
        System.out.println("\trow: " + line);
        System.out.println("\tcol: " + col);
        addError(lexeme, line, col, " ");
    }

    //Metodo que se llama en el momento en que ya no es posible una recuperacion
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        String lexeme = s.value.toString();
        int line = s.left;
        int col = s.right;
        System.out.println("ERROR SINTACTICO NO RECUPERABLE");
        System.out.println("\tLexeme: " + lexeme);
        System.out.println("\trow: " + line);
        System.out.println("\tcol: " + col);
        addError(lexeme, line, col, " ");
    }

    private void addError(String lexeme, int line, int col, String description){
        ErrorsLP.addError(lexeme, line, col, "Error Sintáctico", description);
    }

    private void addParameterError(String type, int line, int col){
        addError(type, line, col, "Parametro " + type + " duplicado");
    }
    private void addPError(String type, int line, int col){
        addError(type, line, col, "Parametro " + type + " no esperado");
    }
    protected int error_sync_size() {
            return 1;
    }
:}

terminal
        DB_USER, DB_TRIVIA,
        ID_TRIVIA, TIEMPO_PREGUNTA, USUARIO_CREACION, TEMA,
        ID, TRIVIA, CLASE, INDICE, TEXTO_VISIBLE, OPCIONES, FILAS, COLUMNAS, RESPUESTA,
        CAMPO_TEXTO, AREA_TEXTO, CHECKBOX, RADIO, FICHERO, COMBO, NONE,
        LPAREN, RPAREN, LBRACE, RBRACE,
        ESTRUCTURA, DATOS_RECOPILADOS, TIEMPO_TOTAL, PUNTEO, ESTADO
        ;

terminal String STRING;

terminal String USUARIO, PASSWORD, INSTITUCION, NOMBRE, FECHA_CREACION, FECHA_MODIFICACION,
                COLON, COMMA;

terminal Integer DIGIT;


non terminal s, db_user, db_trivia;

non terminal User user;

non terminal ArrayList<User> users;

non terminal String username, password, name, institution, creation_date, update_date;

non terminal Trivia trivia;

non terminal ArrayList<Trivia> trivias;

non terminal String id_triva, topic, creation_user;

non terminal Integer question_time;

non terminal Component component;

non terminal ArrayList<Component> components, structure;

non terminal String id_component, param_trivia, visible_text;

non terminal ArrayList<String> options, answer;

non terminal Integer clase, class_type, lines, columns;

non terminal CollectedData data;

non terminal ArrayList<CollectedData> collected_data, datas;

non terminal Integer total_time, score, index;

non terminal String done;


start with s;

s ::=  db_user
    | db_trivia
    ;

db_user ::= DB_USER LPAREN users:us RPAREN:f
        {:
        finalPos = new Position(fleft, fright);
        users = us;
        :}
        | DB_USER LPAREN RPAREN:f
        {:
        finalPos = new Position(fleft, fright);
        users = new ArrayList<>();
        :}
        ;


users ::= user:u
        {:
        ArrayList<User> users = new ArrayList<>();
        users.add(u);
        RESULT = users;
        :}
        | users:us COMMA user:u
        {:
        us.add(u);
        RESULT = us;
        :}
        ;

user ::= LBRACE:b1 username:u password:p name:n institution:i creation_date:c update_date:e RBRACE:b2
        {:
        positions.add(new Position(b1left, b1right, b2left, b2right));

        User usr = new User(u,p,n,i);
        if(c.equals("null")) usr.setCreateDate(null);
        else usr.setCreateDateString(c);

        if(e.equals("null")) usr.setUpdateDate(null);
        else usr.setUpdateDateString(e);
        System.out.println(usr);
        idsUser.add(u);
        RESULT = usr;
        :}
        ;

username ::= USUARIO COLON STRING:s COMMA
            {: RESULT = s.replaceAll("\"", ""); :};

password ::= PASSWORD COLON STRING:s COMMA
            {: RESULT = s.replaceAll("\"", ""); :};

name ::= NOMBRE COLON STRING:s COMMA
        {: RESULT = s.replaceAll("\"", ""); :};

institution ::= INSTITUCION COLON STRING:s COMMA
                {: RESULT = s.replaceAll("\"", ""); :};

creation_date ::= FECHA_CREACION COLON STRING:s COMMA
                {: RESULT = s.replaceAll("\"", ""); :};

update_date ::= FECHA_MODIFICACION COLON STRING:s
                {: RESULT = s.replaceAll("\"", ""); :};


db_trivia ::= DB_TRIVIA LPAREN trivias:ts RPAREN:f
            {:
            finalPosTrivia = new Position(fleft, fright);
            trivias = ts;
            :}
            | DB_TRIVIA LPAREN RPAREN:f
            {:
            finalPosTrivia = new Position(fleft, fright);
            trivias = new ArrayList<>();
            :}
            ;

trivias ::= trivia:t
            {:
            ArrayList<Trivia> trivias = new ArrayList<>();
            trivias.add(t);
            RESULT = trivias;
            :}
            | trivias:ts COMMA trivia:t
            {:
            ts.add(t);
            RESULT = ts;
            :}
            ;

trivia ::= LBRACE:b1 id_triva:i name:n topic:t question_time:q creation_user:c creation_date:cd structure:s datas:d RBRACE:b2
            {:
            positionsTrivia.add(new Position(b1left, b1right, b2left, b2right));
            RESULT = new Trivia(i, n, t, q, c, cd, s, d);
            :}
            ;

id_triva ::= ID_TRIVIA COLON STRING:s COMMA
            {:
            idsTrivia.add(s.replaceAll("\"",""));
            RESULT = s.replaceAll("\"", "");
            :};

topic ::= TEMA COLON STRING:s COMMA
        {: RESULT = s.replaceAll("\"", ""); :};

question_time ::= TIEMPO_PREGUNTA COLON DIGIT:d COMMA
                {: RESULT = d; :};

creation_user ::= USUARIO_CREACION COLON STRING:s COMMA
                {: RESULT = s.replaceAll("\"", ""); :};

structure ::= ESTRUCTURA COLON LPAREN:i components:c RPAREN:f COMMA
            {:
            String t = idsTrivia.get(idsTrivia.size()-1);
            positionsComponents.put(t, new Position(ileft, iright, fleft, fright-1));
            finalPosComponent.put(t, new Position(fleft, fright-1));
            RESULT = c;
            :}
            | ESTRUCTURA COLON LPAREN:i RPAREN:f COMMA
            {:
            String t = idsTrivia.get(idsTrivia.size()-1);
            positionsComponents.put(t, new Position(ileft, iright, fleft, fright-1));
            finalPosComponent.put(t, new Position(fleft, fright-1));
            RESULT = new ArrayList<Component>();
            :}
            ;

components ::= component:c
            {:
            ArrayList<Component> components = new ArrayList<>();
            components.add(c);
            RESULT = components;
            :}
            | components:cs COMMA component:c
            {:
            cs.add(c);
            RESULT = cs;
            :}
            ;

component ::= LBRACE:b1 id_component:i param_trivia:t clase:c index:in visible_text:v options:o lines:l columns:col answer:a RBRACE:b2
            {:
            Component component = new Component(i, t, c, in, v, a);
            if(!o.equals("null")) component.setOptions(o);

            //Agregar ids del componente
            if(!idsComponent.containsKey(t)){
                idsComponent.put(t, new ArrayList<>());
            }
            idsComponent.get(t).add(i);

            RESULT = component;
            :}
            ;

id_component ::= ID COLON STRING:s COMMA
            {: RESULT = s.replaceAll("\"", ""); :};

param_trivia ::= TRIVIA COLON STRING:s COMMA
            {: RESULT = s.replaceAll("\"", ""); :};

clase ::= CLASE COLON class_type:c COMMA
            {: RESULT = c; :};

index ::= INDICE COLON DIGIT:d COMMA
            {: RESULT = d; :};

visible_text ::= TEXTO_VISIBLE COLON STRING:s COMMA
            {: RESULT = s.replaceAll("\"", ""); :};

options ::= OPCIONES COLON STRING:s COMMA
            {:
            String op =  s.replaceAll("\"", "");
            String[] parts = op.split("\\|");
            RESULT = new ArrayList<String>(Arrays.asList(parts));
            :};

lines ::= FILAS COLON DIGIT:d COMMA
        {: RESULT = d; :};

columns ::= COLUMNAS COLON DIGIT:d COMMA
        {: RESULT = d; :};

answer ::= RESPUESTA COLON STRING:s
            {:
            String op =  s.replaceAll("\"", "");
            String[] parts = op.split("\\|");
            RESULT = new ArrayList<String>(Arrays.asList(parts));
            :};

class_type ::= CAMPO_TEXTO      {: RESULT = Component.CAMPO_TEXTO; :}
                | AREA_TEXTO    {: RESULT = Component.AREA_TEXTO; :}
                | CHECKBOX      {: RESULT = Component.CHECKBOX; :}
                | RADIO         {: RESULT = Component.RADIO; :}
                | FICHERO       {: RESULT = Component.FICHERO; :}
                | COMBO         {: RESULT = Component.COMBO; :}
                | NONE          {: RESULT = 0; :}
                ;

datas ::= DATOS_RECOPILADOS COLON LPAREN RPAREN
        {:
        RESULT = new ArrayList<CollectedData>();
        :}
        | DATOS_RECOPILADOS COLON LPAREN collected_data:cd RPAREN
        {:
        RESULT = cd;
        :}
        ;

collected_data ::= data:d
                {:
                ArrayList<CollectedData> collectedDataList = new ArrayList<>();
                collectedDataList.add(d);
                RESULT = collectedDataList;
                :}
                | collected_data:cd COMMA data:d
                {:
                cd.add(d);
                RESULT = cd;
                :}
                ;

data ::= LBRACE username:u param_trivia:t total_time:ti done:d score:s RBRACE
        {:
        boolean b = false;
        if(d.equals("Completado")) b = true;
        if(d.equals("Fallo")) b = false;
        RESULT =  new CollectedData(u, t, ti, s, b);
        :}
         ;

total_time ::= TIEMPO_TOTAL COLON DIGIT:d COMMA
                {: RESULT = d; :};


done ::= ESTADO COLON STRING:s COMMA
            {: RESULT = s.replaceAll("\"", ""); :};


score ::= PUNTEO COLON DIGIT:d
        {: RESULT = d; :};

